SConsignFile()

######################################################################
# Create root environments
######################################################################
env = Environment()

######################################################################
# Create environments
######################################################################
host_env = env.Clone()
target_env = env.Clone(CC = 'arm-elf-gcc',
                       AR = 'arm-elf-ar',
                       RANLIB = 'arm-elf-ranlib',
                       ASFLAGS = ['-g', '-mcpu=arm966e-s'],
                       CCFLAGS = ['-Wall', '-g', '-O0', '-mcpu=cortex-m3'],
                       CPPDEFINES = ['TARGET_IS_TEMPEST_RB1'],
                       CPPPATH = ['#/include',
                                  '#/../include',
                                  '#/../../../include'],
                       LIBPATH = ['..'],
                       LIBS = ['gyros-stellaris', 'gcc'],
                       LINKFLAGS = ['-nostdlib', '-Wl,--nmagic',
                                    '-Tstellaris.ld'],
                       OBJPREFIX = '.stellaris-',
                       PROGSUFFIX = '.elf')
target_env.PrependENVPath('PATH', '/opt/arm-elf/gcc-m3/bin')

Export("host_env", "target_env")

######################################################################
# Include subtargets
######################################################################
SConscript('../SConscript')

######################################################################
# Test program
######################################################################
Default(target_env.Program('test', ['test.c', 'hwinit.c', 'startup.c']))
